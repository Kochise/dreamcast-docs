<HTML>
<HEAD>
<TITLE>Mega Records Example - Using Script to generate Structured Graphics</Title>
<SCRIPT Language="VBScript">
<!--
Dim i
Dim Rotation
Dim Lib
Dim DRS
Dim winsizew
Dim winsizeh

Sub Window_OnLoad
	i=0
	Rotation = 0
	Call Seq("1").Play
End Sub

Sub Seq_OnInit
	Call seq("1").at(0.000, "Gen", 20, 0.150, 1)
	Call seq("1").at(0.000, "Rot", -1, 0.030, 1)
	Call seq("1").at(3.500, "ShowMegaRecords")
	Call seq("1").at(5.000, "MoveMegaRecords", 15, 0.020)
End Sub

Sub ShowMegaRecords
	Mega.style.visibility = "visible"
	Records.style.visibility = "visible"
End Sub

Sub MoveMegaRecords
	Star.style.left = Star.style.posleft + 6
	Star.style.top = Star.style.postop - 7
	Call Star.scale(.94,.94,.94)

	Mega.style.left = mega.style.posleft - 15
	Mega.style.top = mega.style.postop - 8
	Call Mega.Scale(.95,.95,.95)

	Records.style.left = Records.style.posLeft - 15
	Records.style.top = Records.style.posTop - 11
	Call Records.Scale(.95,.95,.95)
End Sub

Sub Gen
	Set Lib = Star.Library
	Set DRS = Star.DrawSurface
	Call DRS.FillStyle(0)
	Call DRS.LineDashStyle(1)
	if i mod 2 = 1 then
		Call DRS.BorderColor(lib.ColorRgb255(200,200,255))
	Else
	 	Call DRS.BorderColor(lib.ColorRgb255(100,100,255))
	End If
	Rotation = i * 18
	Call DRS.PieDegrees(-120, -120, Rotation, Rotation+10 , 240, 240)
	Star.DrawSurface = DRS
	i=i+1
End Sub

Sub Rot
	Call Star.Rotate(-4,2,-2)
End Sub

Sub Window_OnUnLoad
	Call Seq("1").Stop
End Sub
-->
</SCRIPT>
</HEAD>

<BODY BGCOLOR=black TOPMARGIN=0 LEFTMARGIN=0>

<OBJECT ID="Star"
	STYLE="POSITION:ABSOLUTE;HEIGHT:350;WIDTH:350;LEFT:25%;TOP:2%;Z-INDEX:-5"
	CLASSID = "CLSID:369303C2-D7AC-11D0-89D5-00A0C90833E6">
</OBJECT>

<OBJECT ID="Mega"
	STYLE="POSITION:ABSOLUTE;HEIGHT:200;WIDTH:90%;LEFT:5%;TOP:75;visibility:hidden"
	CLASSID = "CLSID:369303C2-D7AC-11D0-89D5-00A0C90833E6">
    <PARAM NAME="Line0001" VALUE="SetFont('Impact',250, 650, 1, 0, 0)">
    <PARAM NAME="Line0002" VALUE="SetLineColor(200, 200, 200)">
    <PARAM NAME="Line0003" VALUE="SetFillColor(200, 200, 200)">
    <PARAM NAME="Line0004" VALUE="SetFillStyle(1)">
    <PARAM NAME="Line0005" VALUE="Text('MEGA', -215, 75, 0)">
</OBJECT>

<OBJECT ID="Records"
	STYLE="POSITION:ABSOLUTE;HEIGHT:100;WIDTH:90%;LEFT:5%;TOP:210;visibility:hidden"
	CLASSID = "CLSID:369303C2-D7AC-11D0-89D5-00A0C90833E6">
    <PARAM NAME="Line0001" VALUE="SetFont('Impact', 72, 700, 1, 0, 0)">
    <PARAM NAME="Line0002" VALUE="SetLineColor(200, 200, 200)">
    <PARAM NAME="Line0003" VALUE="SetFillColor(200, 200, 200)">
    <PARAM NAME="Line0004" VALUE="SetFillStyle(0)">
    <PARAM NAME="Line0005" VALUE="Text('r    e    c    o    r    d    s', -225, 40, 0)">
</OBJECT>

<OBJECT ID="Seq"
	CLASSID="CLSID:B0A6BAE2-AAF0-11d0-A152-00A0C908DB96">
</OBJECT>

</BODY>
</HTML>